-- Define variables
    DECLARE @BranchID INT = 27,
            @CompanyID INT = 27,
            @lFromDate INT = 20000101, 
            @lToDate INT = 20400101;

    -- Main CTE for transaction details
    WITH vTxnDet AS (
        SELECT d.lCompId,
               d.sDocNo,
               d.dtDocDate,
               dd.lItmTyp,
               dd.lItmId,
               dd.lUntId2,
               dd.lLocId,
               dd.lStkBinId,
               dd.sValue1,
               SUM(CASE WHEN d.dtDocDate < @lFromDate THEN dd.dQtyStk ELSE 0 END) AS Opening,
               SUM(CASE WHEN d.dtDocDate >= @lFromDate AND dt.lStkTyp = 0 THEN dd.dQtyStk ELSE 0 END) AS Receipt,
               SUM(CASE WHEN d.dtDocDate >= @lFromDate AND dt.lStkTyp = 1 THEN -dd.dQtyStk ELSE 0 END) AS Issue,
               SUM(dd.dQtyStk) AS Closing,
               SUM(dd.dStkVal) AS ClosingVal
        FROM TXNTYP AS dt
        INNER JOIN TXNHDR AS d ON dt.lTypId = d.lTypId AND dt.lStkTyp < 2
        INNER JOIN TXNDET AS dd ON d.lId = dd.lId
        WHERE d.bDel = 0 
          AND d.lClosed <= 0 
          AND dd.bDel = 0 
          AND dd.cFlag IN ('I', 'A') 
          AND dd.dQtyStk <> 0
          AND d.lCompId IN (27, 28, 9)
          AND d.dtDocDate <= @lToDate
        GROUP BY d.sDocNo,
                 d.dtDocDate,
                 d.lCompId,
                 dd.lItmTyp,
                 dd.lItmId,
                 dd.lUntId2,
                 dd.lLocId,
                 dd.lStkBinId,
                 dd.sValue1
        HAVING NOT (
            SUM(CASE WHEN d.dtDocDate < @lFromDate THEN dd.dQtyStk ELSE 0 END) BETWEEN -0.001 AND 0.001
            AND SUM(CASE WHEN d.dtDocDate >= @lFromDate AND dt.lStkTyp = 0 THEN dd.dQtyStk ELSE 0 END) = 0
            AND SUM(CASE WHEN d.dtDocDate >= @lFromDate AND dt.lStkTyp = 1 THEN -dd.dQtyStk ELSE 0 END) = 0
        )
    )

    -- Final Select statement using the CTE
    SELECT dd.sDocNo AS [Doc No],
           CONVERT(VARCHAR, CONVERT(DATE, CONVERT(VARCHAR(8), dd.dtDocDate)), 105) AS [Date],
           c.sRemarks AS CompanyName,
           it.sName AS [Item Type], 
           i.sCode AS [Item Code], 
           i.sName AS [Item Name], 
           u.sName AS Unit,
           dm.sName AS [Location], 
           sb.sName AS [Stock Location],
           dd.sValue1 AS [Batch No],
           CAST(dd.Opening AS DECIMAL(21, 3)) AS Opening,
           CAST(dd.Receipt AS DECIMAL(21, 3)) AS Receipt,
           CAST(dd.Issue AS DECIMAL(21, 3)) AS Issue,
           CAST(dd.Closing AS DECIMAL(21, 3)) AS Closing,
           CAST(dd.ClosingVal AS DECIMAL(21, 3)) AS ClosingValue,
           icf.mValue1 AS [Inventory Category],
           icf.mValue2 AS [Inventory Subcategory]
    FROM vTxnDet AS dd
    INNER JOIN CMPNY AS c ON dd.lCompId = c.lId
    INNER JOIN ITMTYP AS it ON dd.lItmTyp = it.lTypId AND it.bStkUpd = 1
    INNER JOIN ITMMST AS i ON dd.lItmId = i.lId
    INNER JOIN ITMDET AS t ON dd.lItmTyp = t.lTypId AND dd.lItmId = t.lId AND t.bStkUpd = 1
    LEFT JOIN (
        SELECT dd.lCompId, 
               dd.lItmTyp, 
               dd.lItmId,
               MAX(CASE WHEN icf.lFieldNo = 1 THEN icf.sValue ELSE '' END) AS mValue1,
               MAX(CASE WHEN icf.lFieldNo = 2 THEN icf.sValue ELSE '' END) AS mValue2,
               MAX(CASE WHEN icf.lFieldNo = 3 THEN icf.sValue ELSE '' END) AS mValue3,
               MAX(CASE WHEN icf.lFieldNo = 4 THEN icf.sValue ELSE '' END) AS mValue4,
               MAX(CASE WHEN icf.lFieldNo = 5 THEN icf.sValue ELSE '' END) AS mValue5,
               MAX(CASE WHEN icf.lFieldNo = 6 THEN icf.sValue ELSE '' END) AS mValue6,
               MAX(CASE WHEN icf.lFieldNo = 7 THEN icf.sValue ELSE '' END) AS mValue7,
               MAX(CASE WHEN icf.lFieldNo = 8 THEN icf.sValue ELSE '' END) AS mValue8,
               MAX(CASE WHEN icf.lFieldNo = 9 THEN icf.sValue ELSE '' END) AS mValue9
        FROM vTxnDet AS dd
        INNER JOIN ITMCF AS icf ON dd.lItmTyp = icf.lTypId AND dd.lItmId = icf.lId
        Group by dd.lCompId,dd.lItmTyp, dd.lItmId) as icf on dd.lCompId=icf.lCompId and dd.lItmTyp=icf.lItmTyp and dd.lItmId=icf.lItmId
    LEFT JOIN UNTMST AS u ON dd.lUntId2 = u.lId
    INNER JOIN DIMMST AS dm ON dd.lLocId = dm.lId
    LEFT JOIN STKBIN AS sb ON dd.lStkBinId = sb.lId
    WHERE NOT (dd.Opening = 0 AND dd.Receipt = 0 AND dd.Issue = 0)